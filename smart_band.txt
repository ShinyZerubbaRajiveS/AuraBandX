#include <WiFi.h>
#include <WebServer.h>
#include <Wire.h>
#include <Adafruit_MPU6050.h>
#include <Adafruit_Sensor.h>

// Sensor and Buzzer Definitions
#define LM35_PIN 34          // Analog pin for LM35 temperature sensor
#define HEART_RATE_PIN 35    // Analog pin for heart rate sensor
#define MPU_SDA 21           // I2C SDA pin for MPU-6050
#define MPU_SCL 22           // I2C SCL pin for MPU-6050
#define BUZZER_PIN 19        // Pin for 5V buzzer

Adafruit_MPU6050 mpu;        // MPU-6050 for motion sensing

// WiFi credentials
const char* ssid = "smartband";
const char* password = "12345678";

WebServer server(80);

float temperature = 0, BPM = 0, accelMagnitude = 0;
bool epilepsyDetected = false;
bool fallDetected = false;
uint32_t tsLastReport = 0;
#define REPORTING_PERIOD_MS 1000

// Fall detection thresholds to adjust
#define FALL_IMPACT_THRESHOLD 25.0  // High acceleration indicates a fall
#define FALL_LYING_THRESHOLD 1.0    // Low acceleration indicates lying down after a fall
#define FALL_IMPACT_TIME 500        // Time window for impact detection in milliseconds

unsigned long impactTimestamp = 0;

void setup() {
  Serial.begin(115200);

  // WiFi connection setup
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.print(".");
  }
  Serial.println("WiFi connected");
  Serial.print("Got IP: ");
  Serial.println(WiFi.localIP());

  // Initialize server routes
  server.on("/", handle_OnConnect);
  server.onNotFound(handle_NotFound);
  server.begin();
  Serial.println("HTTP server started");

  // Initialize I2C with custom SDA and SCL pins
  Wire.begin(MPU_SDA, MPU_SCL);

  // Initialize MPU6050
  if (!mpu.begin()) {
    Serial.println("Failed to find MPU6050 chip!");
    while (1) { delay(10); }
  }
  mpu.setAccelerometerRange(MPU6050_RANGE_8_G);
  mpu.setGyroRange(MPU6050_RANGE_500_DEG);
  mpu.setFilterBandwidth(MPU6050_BAND_21_HZ);

  pinMode(HEART_RATE_PIN, INPUT);
  pinMode(LM35_PIN, INPUT);
  pinMode(BUZZER_PIN, OUTPUT);   // Set pin mode for buzzer
}

void loop() {
  server.handleClient();
  updateSensors();
  
  if (millis() - tsLastReport > REPORTING_PERIOD_MS) {
    checkForEpilepsy();
    checkForFall();
    sendToSerialMonitor();
    tsLastReport = millis();
  }
}

void updateSensors() {
  // Read LM35 temperature sensor
  int lm35Value = analogRead(LM35_PIN);
  temperature = (lm35Value * 3.3 / 4095.0) * 100;
  
  // Read heart rate sensor
  int heartValue = analogRead(HEART_RATE_PIN);
  BPM = heartValue * (100.0 / 4095.0);  // Scale value (adjust as necessary)
  
  // Read accelerometer data from MPU-6050
  sensors_event_t a, g, temp;
  mpu.getEvent(&a, &g, &temp);
  accelMagnitude = sqrt(a.acceleration.x * a.acceleration.x + 
                        a.acceleration.y * a.acceleration.y + 
                        a.acceleration.z * a.acceleration.z);
}

void checkForEpilepsy() {
  if (BPM > 90 || temperature > 40.0 || accelMagnitude > 13.0) {
    epilepsyDetected = true;
    beepBuzzer(3000);  // Beep for 3 seconds if epilepsy is detected
  } else {
    epilepsyDetected = false;
  }
}

void beepBuzzer(int duration) {
  digitalWrite(BUZZER_PIN, HIGH);   // Turn buzzer ON
  delay(duration);                  // Keep buzzer on for the duration
  digitalWrite(BUZZER_PIN, LOW);    // Turn buzzer OFF
}

void checkForFall() {
  if (accelMagnitude > FALL_IMPACT_THRESHOLD) {
    impactTimestamp = millis();
  }

  if (millis() - impactTimestamp < FALL_IMPACT_TIME) {
    if (accelMagnitude < FALL_LYING_THRESHOLD) {
      fallDetected = true;
    } else {
      fallDetected = false;
    }
  } else {
    fallDetected = false;
  }
}

void sendToSerialMonitor() {
  Serial.print("Temperature: ");
  Serial.print(temperature);
  Serial.println("°C");

  Serial.print("Heart Rate: ");
  Serial.print(BPM);
  Serial.println(" BPM");

  Serial.print("Acceleration Magnitude: ");
  Serial.print(accelMagnitude);
  Serial.println(" m/s^2");

  if (epilepsyDetected) {
    Serial.println("Epilepsy Detected: YES");
  } else {
    Serial.println("Epilepsy Detected: NO");
  }

  if (fallDetected) {
    Serial.println("Fall Detected: YES");
  } else {
    Serial.println("Fall Detected: NO");
  }

  Serial.println("*");
}

void handle_OnConnect() {
  server.send(200, "text/html", generateHTML());
}

void handle_NotFound() {
  server.send(404, "text/plain", "Not found");
}

String generateHTML() {
  String html = "<html><head><meta http-equiv='refresh' content='3'></head><body>";  // Automatically refresh every 3 seconds
  html += "<h1>Health Band Monitor</h1>";
  html += "<p>Temperature: " + String(temperature) + "°C</p>";
  html += "<p>Heart Rate: " + String(BPM) + " BPM</p>";
  html += "<p>Acceleration: " + String(accelMagnitude) + " m/s^2</p>";
  
  if (epilepsyDetected) {
    html += "<p style='color:red;'>Epilepsy Detected: YES</p>";
  } else {
    html += "<p>Epilepsy Detected: NO</p>";
  }

  if (fallDetected) {
    html += "<p style='color:red;'>Fall Detected: YES</p>";
  } else {
    html += "<p>Fall Detected: NO</p>";
  }
  
  html += "</body></html>";
  return html;
}
